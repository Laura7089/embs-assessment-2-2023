#!/bin/env -S just --justfile

TEST_DIR := "./tests"
BIN_NAME := "main"
SVG_VIEWER := env_var_or_default("BROWSER", "firefox")
FLAME_OUT := `mktemp --tmpdir -u embs_poc_flameXXXX.svg`
RAND_EPOCH := `echo $(($RANDOM % 10))`

run size: (build size)
    time ./{{ BIN_NAME }}
alias r := run

build size epoch=RAND_EPOCH *args="":
    make SIZE={{ size }} EPOCH={{ epoch }} {{ args }} {{ BIN_NAME }}

debug debugger="gdb" *args="": (build "'CFLAGS=-g -O0'")
    "{{ debugger }}" {{ args }} {{ BIN_NAME }}
alias d := debug

test:
    "{{ TEST_DIR }}/run_all.sh"
alias t := test

bench size *args="": (build size)
    hyperfine \
        -N \
        {{ args }} \
        -- \
        ./{{ BIN_NAME }}
alias b := bench

flame size output=FLAME_OUT *args="": (build size RAND_EPOCH)
    time flamegraph \
        -o "{{ output }}" \
        {{ args }} \
        -- \
        ./{{ BIN_NAME }}
    "{{ SVG_VIEWER }}" "{{ output }}"

clean:
    rm -fv {{ BIN_NAME }}
    rm -fv "{{ TEST_DIR }}/test_*"
    rm -fv perf.*
alias c := clean

clean_all: clean
    rm -rf puzzles
