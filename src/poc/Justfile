#!/bin/env -S just --justfile

TEST_DIR := "./tests"
BIN_NAME := "main"
SVG_VIEWER := env_var_or_default("BROWSER", "firefox")
FLAME_OUT := `mktemp --tmpdir -u embs_poc_flameXXXX.svg`

run: build
    ./{{ BIN_NAME }}
alias r := run

build *args="":
    make {{ args }} {{ BIN_NAME }}

debug debugger="gdb" *args="": (build "'CFLAGS=-g -O0'")
    "{{ debugger }}" {{ args }} {{ BIN_NAME }}
alias d := debug

test:
    "{{ TEST_DIR }}/run_all.sh"
alias t := test

bench *args="": build
    hyperfine \
        -N \
        {{ args }} \
        -- \
        ./{{ BIN_NAME }}
alias b := bench

flame output=FLAME_OUT *args="": build
    flamegraph \
        -o "{{ output }}" \
        {{ args }} \
        -- \
        ./{{ BIN_NAME }}
    "{{ SVG_VIEWER }}" "{{ output }}"

clean:
    rm -fv {{ BIN_NAME }}
    rm -fv "{{ TEST_DIR }}/test_*"
    rm -fv perf.*
alias c := clean

clean_all: clean
    rm -rf puzzles
